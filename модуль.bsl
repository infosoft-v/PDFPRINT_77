Перем м_ПринтерPDF;

Функция ПринтерPDFПодключить()
	Перем бПодключен;
	
	бПодключен = 0;

	Попытка
		м_оPDFUtil = СоздатьОбъект("Bullzip.PdfUtil");
		м_ПринтерPDF = м_оPDFUtil.DefaultPrinterName;
		бПодключен = 1;
	Исключение
		Сообщить("Ошибка при подключении PDF принтера Bullzip. " + ОписаниеОшибки(), "!!");
		м_ПринтерPDF = "";
		бПодключен = 0;
	КонецПопытки;	

	Возврат бПодключен;
КонецФункции

Процедура Пауза(чСек)
	Перем сФайлСкрипт, оТекст, оWSC;
	
	сФайлСкрипт = КаталогВременныхФайлов()+ "SleepScript.js";
	оТекст = СоздатьОбъект("Текст");
	оТекст.ДобавитьСтроку(
		"if (WScript.Arguments.Count()==0) { 
		| WScript.Quit();
		|} else {
		| if (isNaN(parseInt(WScript.Arguments(0)))) {
		| WScript.Quit();
		|}} 
		|WScript.Sleep(WScript.Arguments(0));"
	);
	оТекст.Записать(сФайлСкрипт);
	оWSC = СоздатьОбъект("WScript.Shell");
    оWSC.Run("wscript.exe """+сФайлСкрипт+""" "+Строка(чСек*1000), 0, 1);
	
	Если ФС.СуществуетФайл(сФайлСкрипт) = 1 Тогда
		ФС.УдалитьФайл(сФайлСкрипт);
	КонецЕсли;

КонецПроцедуры

Функция ВремяВСекундах(чЧас, чМин, чСек, чДельта = 0)
	Перем чВремя;

	чВремя = чЧас * 60 * 60 + чМин * 60 + чСек + чДельта;
	Если чВремя > 86399 Тогда
		чВремя = чВремя - 86400;
	КонецЕсли;

	Возврат чВремя;
КонецФункции

Функция ОтправитьДокументPDF(оТаблица, сИмяФайла)
	Перем чОриентация, чРезультат, оPDFSetting, сКаталогPDF;
	Перем чЧас,	чМин, чСек,	чфлаг, чТекущее, чСтоп, чФлагФС;

	чРезультат = 0;

	// Нет принтера, не печатаем
	Если ПустаяСтрока(м_ПринтерPDF) = 1 Тогда
		Возврат чРезультат;
	КонецЕсли;

	сКаталогPDF = ПроверитьСоздатьКаталог(ИмяКаталогаОтправки);
	Если сКаталогPDF = "" Тогда
		Сообщить("Не найден каталог для PDF документов.");
		Возврат чРезультат;
	КонецЕсли;

	// Установить параметры PDF принтера
	Попытка
		оPDFSetting = СоздатьОбъект("Bullzip.PdfSettings");

		оPDFSetting.PrinterName = м_ПринтерPDF;
		оPDFSetting.SetValue("Output", сИмяФайла + ".pdf"); 
		оPDFSetting.SetValue("ShowSettings", "never"); 
		оPDFSetting.SetValue("ShowSaveAS", "never"); 
		оPDFSetting.SetValue("ShowProgress", "no"); 
		оPDFSetting.SetValue("ShowProgressFinished", "no"); 
		оPDFSetting.SetValue("ShowPDF", "no"); 
		оPDFSetting.SetValue("ConfirmOverwrite", "no"); 

		оPDFSetting.WriteSettings(1); 
	Исключение
		Сообщить("Ошибка настройки PDF принтера. " + ОписаниеОшибки(), "!!");
		Сообщить("Документ " + сИмяФайла + "не отправлен в ЭДО.");
		Возврат чРезультат;
	КонецПопытки;

	чОриентация = 2; // Альбомная
	оТаблица.Опции(0, 0, 0, 0, "");
	оТаблица.ПараметрыСтраницы(чОриентация,,,,,,,,,1,,м_ПринтерPDF);
	оТаблица.Напечатать(0);

	чЧас = 0;
	чМин = 0;
	чСек = 0;
	ТекущееВремя(чЧас, чМин, чСек);
	чТекущее = ВремяВСекундах(чЧас, чМин, чСек, 0);
	чСтоп = ВремяВСекундах(чЧас, чМин, чСек, 30);
	
	чфлаг = 0;
	Пока чТекущее < чСтоп Цикл
		чФлагФС = 0;
		чФлагФС = ФС.СуществуетФайл(сИмяФайла + ".pdf");	
		Если чФлагФС = 1 Тогда
			чФлаг = 1;
			Прервать;
		КонецЕсли;
		Пауза(0.1);
		ТекущееВремя(чЧас, чМин, чСек);
		чТекущее = ВремяВСекундах(чЧас, чМин, чСек, 0);	
	КонецЦикла;

	Если чФлаг = 1 Тогда
		чРезультат = 1;
	Иначе
		чРезультат = 0;
		Сообщить("Не удалось создать PDF документ " + сИмяФайла + ".pdf");
	КонецЕсли;
	
	Возврат чРезультат;
КонецФункции
